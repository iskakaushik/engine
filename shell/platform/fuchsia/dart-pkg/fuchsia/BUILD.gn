# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fuchsia/sdk.gni")

template("fuchsia_sources") {
  product = defined(invoker.product) && invoker.product
  product_suffix = ""
  if (product) {
    product_suffix = "_product"
  }

  source_set(target_name) {
    sources = [
      "sdk_ext/fuchsia.cc",
      "sdk_ext/fuchsia.h",
    ]

    public_deps = [
      "$fuchsia_sdk_root/fidl:fuchsia.sys",
      "$fuchsia_sdk_root/pkg:async-cpp",
      "$fuchsia_sdk_root/pkg:fidl_cpp",
      "$fuchsia_sdk_root/pkg:zx",
      "//flutter/third_party/tonic",  # TODO(dworsham): Figure out ownership
      "//third_party/dart/runtime:dart_api",
    ]

    deps = [
      "//flutter/shell/platform/fuchsia/dart-pkg/zircon:zircon$product_suffix",
      "//flutter/shell/platform/fuchsia/utils:utils$product_suffix",
    ]
  }
}

fuchsia_sources("fuchsia_product") {
  product = true
}

fuchsia_sources("fuchsia") {
  product = false
}
